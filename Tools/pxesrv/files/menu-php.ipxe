#!ipxe

set boot-url http://${next-server}

# Setup some basic convenience variables
set menu-timeout 5000
set submenu-timeout ${menu-timeout}

# Ensure we have menu-default set to something
isset ${menu-default} || set menu-default exit

######## MAIN MENU ###################
:start
menu Welcome to iPXE's Boot Menu
item
item sanboot_php    look for iso files and boot via sanboot
item memdisk_php    look for iso files and boot via memdisk
item wimboot_php    look for wim files and boot via wimboot 
item --gap -- ------------------------------ Advanced ---------------------------------
item config       Configure settings
item shell      Enter iPXE shell
item reboot     Reboot
item exit       Exit (boot local disk)
choose --default exit --timeout 30000 target && goto ${target}

########## UTILITY ITEMS ####################
:shell
echo Type exit to get the back to the menu
shell
set menu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit
 
:config
config
goto start
###########################################################
:sanboot_php
chain ${boot-url}/sanboot.php?files// || goto failed
goto start 

:memdisk_php
chain ${boot-url}/memdisk.php?files// || goto failed
goto start 

:wimboot_php
chain ${boot-url}/wimboot.php || goto failed
#chain ${boot-url}/wimboot.php?files// || goto failed
goto start 

goto start
